Alphabet

 а б в г д е ё ж з и й к л м н ӈ о п р с т у ф х һ ц ч ш щ ъ ы ь э ю я
 А Б В Г Д Е Ё Ж З И Й К Л М Н Ӈ О П Р С Т У Ф Х Һ Ц Ч Ш Щ Ъ Ы Ь Э Ю Я

 %{ː%}:̄

 %{L%}:л %{L%}:р
 %{G%}:г %{G%}:к %{G%}:ӈ
 %{Г%}:г %{Г%}:к %{Г%}:ӈ %{Г%}:в
 %{D%}:д %{D%}:т %{D%}:ч
 %{A%}:а %{A%}:э %{A%}:о
 %{Я%}:а %{Я%}:э %{Я%}:о %{Я%}:я %{Я%}:е %{Я%}:ё 
 %{V%}:в %{V%}:м
 %{В%}:в %{В%}:м  %{В%}:б
 %{N%}:н %{N%}:т

 %{r%}:р %{r%}:т %{r%}:л %{r%}:н %{r%}:0
 %{j%}:0 %{j%}:й
 %{i%}:0 %{i%}:и %{i%}:ы
 %{ы%}:0 %{ы%}:ы
 %{g%}:0 %{g%}:г %{g%}:к
 %{u%}:0 %{u%}:у
 %{ъ%}:0 %{ъ%}:ъ

 %{а%}:0 %{э%}:0 
 %{ч%}:0 %{д%}:0
 %{t%}:0 %{☭%}:0
 %{n%}:0 %{ь%}:0 

 %{Ч%}:ч %{Ч%}:с %{Ч%}:ш %{Ч%}:щ
 %{О%}:э %{О%}:о %{О%}:ё %{О%}:а
 %{Б%}:б %{Б%}:п %{Б%}:к
 %{Э%}:а %{Э%}:э %{Э%}:о
 %{Е%}:я %{Е%}:е %{Е%}:ё


 %>:0  
 %-
;

Sets

Vow = а е ё и о у ы э ю я 
      А Е Ё И О У Ы Э Ю Я ;

Vow1Long =  е о ё       !а 
            Е О Ё ;     !А 

Vow1Short = а я е 
            А Я Е ;

Vow2Long =  а я и у ю э
            А Я И У Ю Э;

Vow2Short = э
            Э ;

VowNeutral = и ы у ю
             И Ы У Ю ;

Cons = б в г д ж з й к л м н ӈ п р с т ф х һ ц ч ш щ 
       Б В Г Д Ж З Й К Л М Н Ӈ П Р С Т Ф Х Һ Ц Ч Ш Щ ;

Nasal = м н ӈ
        М Н Ӈ ; 

VoicelessCns = к с т п ;

Rules 

"Suffix vowel {i} realised as и"
%{i%}:и <= %{д%}: %>: _ [ в у н | т ы н ] ;

!# дю{а}>{G}ит{д}>{i}вун
!# дю0гид00ивун
!@ д ю {а}:0 >:0 {G}:г и т:д {д}:0 >:0 {i}:и в у н

"Remove ь before suffix vowel {i}"
ь:0 <=> _ :0* %{i%}: ;

!# автомобиль>{i}л
!# автомобил00ил
!@ а в т о м о б и л ь>0 >:0 {i}:и л

"Remove й before suffix vowel {i}"
й:0 <=> _ :0* %{i%}:и ; ! should %{i%} be with %{ː%} ?

!# учёнай>{i}л
!# учёна00ил
!@ у ч ё н а й:0 >:0 {i}:и л

"Remove suffix vowel {i}"
%{i%}:0 <=>    :Vow ( %{ː%}: ) :0* _ ;
             :Vow ( %{ː%}: ) :0* т _ к и ;
                             %>: ӈ _ %>: Cons+ Vow+ ;
                %>: %{D%}: у к %>: _ Cons+ Vow+ ;
                               %>: _ [ в у н | т ы н ] ;
                     [ н | р ] %>: _ т:д %{д%}: %>: %{i%}:и ;
                         %{u%}: >: _ ;
             except
                           й:0 :0* _ ;
                        %{д%}: :0* _ ;

!# мо{ː}>{i}л
!# мо̄00л
!@ м о {ː}:̄ >:0 {i}:0 л

!# мо{ː}>т{i}ки
!# мо̄00т0ки
!@ м о {ː}:̄ >:0 т {i}:0 к и

!# орон>{D}ук>{i}вун
!# орон0дук00вун
!@ о р о н >:0 {D}:д у к >:0 {i}:0 в у н

"Suffix vowel {i} realised as ы after т or д"
%{i%}:ы <=> [ т | д ] :0* _ ;
              :Cons :0* т _ к и ;
               %{ы%}: %>: _ ;
        except 
               %{д%}: %>: _ ;
               %{ч%}: %>: _ ;
               %{ь%}: :0* _ ;
    :Vow ( %{ː%}: ) :0* т _ к и ;

!# дэт>{i}кли
!# дэт0ыкли
!@ д э т >:0 {i}:ы к л и

!# хуна{ː}т{д}>{i}л
!# хуна̄д00ил
!@ х у н а {ː}:̄ т:д {д}:0 >:0 {i}:и л

!# чаӈит{ь}>{i}л
!# чаӈит00ил
!@ ч а ӈ и т {ь}:0 >:0 {i}:и л

"Remove н before the sign {n}"
н:0 <=> _ %{n%}: ;

"Assimilate possessive to plural"
%{L%}:Cx <=> Cx %>: _ ;
       where Cx in ( л р ) ;

!# хутэ>{i}л>{L}и
!# хутэ00л0ли
!@ х у т э >:0 {i}:0 л >:0 {L}:л и

!# оро>р>{L}ун
!# оро0р0рун
!@ о р о >:0 р >:0 {L}:р у н

"Consonant assimilation in suffixes"
%{G%}:Cy <=> Cx :0* _ ;
       where Cx in ( к с т п м н ӈ ) 
             Cy in ( к к к к ӈ ӈ ӈ ) 
       matched ; 

!# дэт>{G}ит
!# дэт0кит	
!@ д э т >:0 {G}:к и т

!# курим>{G}ит
!# курим0ӈит
!@ к у р и м >:0 {G}:ӈ и т

"Consonant assimilation in suffixes"
%{Г%}:Cy <=> Cx :0* _ ;
       where Cx in ( к с т п м н ӈ г ) 
             Cy in ( к к к к ӈ ӈ ӈ в ) 
       matched ; 

"Voicing assimilation of {D}"
%{D%}:т <=> [ :VoicelessCns | %{☭%}: | %{t%}: ] :0* ( %- ) _ ;

!# тар>-{D}{A}{ː}
!# тар0-да̄
!@ т а р >:0 - {D}:д {A}:а {ː}:̄

!# дэт>{D}{u}ли	
!# дэт0тули
!@ д э т >:0 {D}:т {u}:у л и

"{D} realises as ч"
%{D%}:ч <=> [ :VoicelessCns | %{☭%}: | %{t%}: ] :0* %>: _ %{Я%}: ;

!# эе{ː}{э}т>{D}{Я}>м
!# эе̄0т0чэ0м
!@ э е {ː}:̄ {э}:0 т >:0 {D}:ч {Я}:э >:0 м

"Remove {D} after vowel-ending stem when used in <locall> and <prl> cases"
%{D%}:0 <=> :Vow ( %{ː%}: | :0* ) %>: _ %{u%}: ;

!# голо>{D}{u}ли
!# голо000ли
!@ г о л о >:0 {D}:0 {u}:0 л и

!"{u} realises as у"
!%{u%}:у <= %{В%}:в _ %>: %{i%}: ;

!# тэты>{В}{u}>{i}м
!# тэты0ву0{i}м
!@ т э т ы >:0 {В}:в {u}:у >:0 {i}:0 м

"Remove {u} after vowel-ending stem when used in <locall> and <prl> cases and after {V}:в"
%{u%}:0 <=> %>: [ %{D%}:0 | %{V%}:в ] _ ;
                              %{В%}:в _ %>: ;
        except
                              %{В%}:в _ %>: %{i%}: ;
!# голо>{D}{u}ли
!# голо000ли
!@ г о л о >:0 {D}:0 {u}:0 л и

!# тэты>{В}{u}>{r}{A}>н
!# тэты0в00ра0н
!@ т э т ы >:0 {В}:в {u}:0 >:0 {r}:р {A}:э >:0 н

"Voicing assimilation of {g}"
%{g%}:к <=> [ :VoicelessCns | %{☭%}: | %{t%}: ] %>: _ ; 

"Remove {g} after Vow"
%{g%}:0 <=> :Vow %>: _ ; 

"{D}:д after Nasal in <comp> and <sup> suffixes"
%{D%}:д <= :Nasal :0* (%-) _ %{ы%}: ;

"ы appears after {D}:д in <comp> and <sup> suffixes"
%{ы%}:ы <= %{D%}:д _ ; 

!# баян>{D}{ы}м{A}р	
!# баян0дымар
!@ б а я н >:0 {D}:д {ы}:ы м {A}:а р

"т turns into ч"
т:ч <=> _ %{ч%}: %>: :и ;

!# таткит{ч}>{i}л	
!# таткич00ил
!@ т а т к и т:ч {ч}:0 >:0 {i}:и л

"т turns into д"
т:д <=> _ %{д%}: %>: :и ;

!# орон>{G}ит{д}>{i}вун
!# орон0ӈид00ивун
!@ о р о н >:0 {G}:ӈ и т:д {д}:0 >:0 {i}:и в у н

"Nasal assimilation of {V}" 
%{V%}:м <=> :Nasal :0* (%-) _ ; 

!# орон>{V}{A}>{i}в
!# орон0мо00в
!@ о р о н >:0 {V}:м {A}:о >:0 {i}:0 в

"Nasal assimilation of {В}" 
%{В%}:м <=> :Nasal :0* %>: _ ; 

"{В} realises as б after л" 
%{В%}:б <=> :л :0* %>: _ ; 

"Assimilation of {r}"
%{r%}:Cy <=> Cx: :0* _ ;
         where Cx in ( н к с т п л м ӈ )
               Cy in ( 0 т т т т л н н )
         matched ;

!# тэт>{r}{A}
!# тэт0тэ
!@ т э т >:0 {r}:т {A}:э

!# у{а}м>{r}{A}
!# у0м0на
!@ у {а}:0 м >:0 {r}:н {A}:а

"Assimilation of {N} before suffixes starting with т, ч"
%{N%}:т <=> _ %>: [ т | ч ] ; 

"Vowel harmony for {A}: a is chosen"
%{A%}:а <=> [ :Vow1Long %{ː%}: | :Vow1Short ] [ :VowNeutral | :Cons | %{ː%}: ]*/[ :0 | %- ]  _ ;
                                                  [ :о | :ё ] ?* [ :Vow - [ :о | :ё ] ] ?*  _ ;
                                                                                 %{а%}: ?*  _ ;
                                                                            except
                                                                                 %{э%}: ?*  _ ;

!# гара>{V}{A}
!# гара0ва
!@ г а р а >:0 {V}:в {A}:а

!# мо{ː}>{V}{A}
!# мо̄0ва	
!@ м о {ː}:̄ >:0 {V}:в {A}:а

!# адыл>{V}{A}
!# адыл0ва
!@ а д ы л >:0 {V}:в {A}:а

"Vowel harmony for {A}: э is chosen"
%{A%}:э <=> [ :Vow2Long %{ː%}: | :Vow2Short ] [ :VowNeutral | :Cons | %{ː%}: ]*/[ :0 | %- ]  _ ;
                                                                                  %{э%}: ?* _ ;
                                                                          except
                                                                                  %{а%}: ?* _ ;
                                                                                       о ?* _ ;

!# дэт>{V}{A}
!# дэт0вэ
!@ д э т >:0 {V}:в {A}:э

!# га{э}г>{V}{A}
!# га0г0вэ
!@ г а {э}:0 г >:0 {V}:в {A}:э

"Vowel harmony for {A}: о is chosen"
%{A%}:о <=> [ :о | :ё ] [ о | ё | :Cons ]*/:0 (%-) _ ;
        except
          [ :о | :ё ] ?* [ :Vow - [ :о | :ё ] ] ?* _ ;


!# орон>{V}{A}
!# орон0мо	
!@ о р о н >:0 {V}:м {A}:о

"Non-yotised vowel harmony for {Я}: a is chosen"
%{Я%}:а <=>             [ %{а%}: | Vow1Long: %{ː%}: | Vow1Short: ] [ :VowNeutral | :Cons | %{ː%}: ]* н %>: %{ъ%}:0 _ ;
             [ %{а%}: | Vow1Long: %{ː%}: | Vow1Short: ] [ :VowNeutral | :Cons | %{ː%}: ]* %>: %{i%}: л %>: %{ъ%}:0 _ ;
                    [ %{а%}: | Vow1Long: %{ː%}: | Vow1Short: ] [ :VowNeutral | :Cons | %{ː%}: ]* %>: р %>: %{ъ%}:0 _ ;
            [ %{а%}: | Vow1Long: %{ː%}: | Vow1Short: ] [ :VowNeutral | :Cons | %{ː%}: ]* %>: [ %{D%}:ч | %{D%}:т ] _ ;
                                     [ :о | :ё ] ?* [ :Vow - [ :о | :ё ] ] ?* [ %{ъ%}:0 | [ %{D%}:ч | %{D%}:т ] ] _ ;

!# мо{ː}>{i}л>{ъ}{Я}
!# мо̄00л00а	
!@ м о {ː}:̄ >:0 {i}:0 л >:0 {ъ}:0 {Я}:а

"Non-yotised vowel harmony for {Я}: э is chosen"
%{Я%}:э <=>             [ %{э%}: | Vow2Long: %{ː%}: | Vow2Short: ] [ :VowNeutral | :Cons | %{ː%}: ]* н %>: %{ъ%}:0 _ ;
             [ %{э%}: | Vow2Long: %{ː%}: | Vow2Short: ] [ :VowNeutral | :Cons | %{ː%}: ]* %>: %{i%}: л %>: %{ъ%}:0 _ ;
                    [ %{э%}: | Vow2Long: %{ː%}: | Vow2Short: ] [ :VowNeutral | :Cons | %{ː%}: ]* %>: р %>: %{ъ%}:0 _ ;
            [ %{э%}: | Vow2Long: %{ː%}: | Vow2Short: ] [ :VowNeutral | :Cons | %{ː%}: ]* %>: [ %{D%}:ч | %{D%}:т ] _ ;

!# хулуку{э}н>{ъ}{Я}
!# хулуку0н00э
!@ х у л у к у {э}:0 н >:0 {ъ}:0 {Я}:э

!# эе{ː}{э}т>{D}{Я}>м
!# эе̄0т0чэ0м
!@ э е {ː}:̄ {э}:0 т >:0 {D}:ч {Я}:э >:0 м

"Non-yotised vowel harmony for {Я}: о is chosen"
%{Я%}:о <=>             [ :о | :ё ] :Cons* н %>: %{ъ%}:0 _ ;
             [ :о | :ё ] :Cons* %>: %{i%}: л %>: %{ъ%}:0 _ ;
                    [ :о | :ё ] :Cons* %>: р %>: %{ъ%}:0 _ ;
            [ :о | :ё ] :Cons* %>: [ %{D%}:ч | %{D%}:т ] _ ;

!# орон>{ъ}{Я}>{i}в
!# орон00о00в
!@ о р о н >:0 {ъ}:0 {Я}:о >:0 {i}:0 в

"Yotised vowel harmony for {Я}: я is chosen"
%{Я%}:я <=>  [ Vow1Long: %{ː%}: | Vow1Short: ] [ :VowNeutral | :Cons ]*/[ %>: | %{ː%}: ] ( %{ъ%}: | %{ь%}: ) _ ; 
                                                                                                  %{а%}: ?* _ ;
                                                      except 
                                                                                              н %>: %{ъ%}:0 _ ;
                                                                                   %>: %{i%}: л %>: %{ъ%}:0 _ ;
                                                                                          %>: р %>: %{ъ%}:0 _ ;
                                                                                  %>: [ %{D%}:ч | %{D%}:т ] _ ;

!# гара>{ъ}{Я}	
!# гара00я
!@ г а р а >:0 {ъ}:0 {Я}:я

!# бака>{D}{Я}>м
!# бака0дя0м	
!@ б а к а >:0 {D}:д {Я}:я >:0 м

"Yotised vowel harmony for {Я}: е is chosen"
%{Я%}:е <=>  [ Vow2Long: %{ː%}: | Vow2Short: ] [ :VowNeutral | :Cons ]*/[ %>: | %{ː%}: ] ( %{ъ%}: | %{ь%}: ) _ ;
                                                                                                   %{э%}: ?* _ ;
                                                      except 
                                                                                               н %>: %{ъ%}:0 _ ;
                                                                                    %>: %{i%}: л %>: %{ъ%}:0 _ ;
                                                                                           %>: р %>: %{ъ%}:0 _ ;
                                                                                   %>: [ %{D%}:ч | %{D%}:т ] _ ;

!# илэ>{ъ}{Я}	
!# илэ00е
!@ и л э >:0 {ъ}:0 {Я}:е

!# суру{э}>{D}{Я}>м
!# суру00де0м	
!@ с у р у {э}:0 >:0 {D}:д {Я}:е >:0 м

"Yotised vowel harmony for {Я}: ё is chosen"
%{Я%}:ё <=>  [ :о | :ё ] :Cons*/[ %>: ] ( %{ъ%}: | %{ь%}: ) _ ;
                              
              except 
                                              н %>: %{ъ%}:0 _ ;
                                   %>: %{i%}: л %>: %{ъ%}:0 _ ;
                                          %>: р %>: %{ъ%}:0 _ ;
                                  %>: [ %{D%}:ч | %{D%}:т ] _ ;
 
!# голо>{ъ}{Я}
!# голо00ё
!@ г о л о >:0 {ъ}:0 {Я}:ё

"ъ appears after Cons"
%{ъ%}:ъ <=> :Cons :0* %>: _ ;
         except
                    н %>: _ ;
         %>: %{i%}: л %>: _ ;
                %>: р %>: _ ;

!# уму{а}к>{ъ}{Я}
!# уму0к0ъя
!@ у м у {а}:0 к >:0 {ъ}:ъ {Я}:я

!# дэт>{ъ}{Я}	
!# дэт0ъе
!@ д э т >:0 {ъ}:ъ {Я}:е

"Assimilation of %{Б%}"
%{Б%}:б <=                                          _ [ %{A%}: | %{Э%}: ] [ р | л ];
         except
           [ :VoicelessCns | %{☭%}: | %{t%}: ] :0* _ [ %{A%}: | %{Э%}: ] [ р | л ];
